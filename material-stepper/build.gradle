apply plugin: 'com.android.library'
apply plugin: 'maven'
apply from: '../material-stepper/code_coverage.gradle'
apply from: '../config/quality/quality.gradle'

ext {
    bintrayRepo = 'maven'
    bintrayName = POM_ARTIFACT_ID

    publishedGroupId = POM_GROUP_ID
    libraryName = 'Android Material Stepper'
    artifact = POM_ARTIFACT_ID

    libraryDescription = 'This library allows to use Material steppers inside Android applications.'

    siteUrl = 'https://github.com/stepstone-tech/android-material-stepper'
    gitUrl = 'https://github.com/stepstone-tech/android-material-stepper.git'

    libraryVersion = POM_VERSION

    developerId = 'zawadz88'
    developerName = 'Piotr Zawadzki'
    developerEmail = 'piotr.zawadzki@stepstone.com'

    licenseName = 'The Apache Software License, Version 2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    allLicenses = ["Apache-2.0"]
}

android {
    compileSdkVersion project.androidCompileSdkVersion
    buildToolsVersion ("$androidBuildToolsVersion")

    defaultConfig {
        minSdkVersion project.androidMinSdkVersion
        targetSdkVersion project.androidTargetSdkVersion
        vectorDrawables.useSupportLibrary = true

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    testOptions {
        //workaround so that code coverage is working with Robolectric: http://stackoverflow.com/a/35769943/973379
        unitTests.all {
            jacoco {
                includeNoLocationClasses = true
            }
        }
    }

    lintOptions {
        warningsAsErrors true
    }
}

dependencies {
    compile ("com.android.support:appcompat-v7:$androidSupportLibraryVersion")

    testCompile("junit:junit:$junitVersion")
    testCompile("org.mockito:mockito-core:$mockitoVersion")
    testCompile("com.squareup.assertj:assertj-android:$assertjVersion")
    testCompile("org.robolectric:robolectric:$robolectricVersion") {
        exclude group: 'commons-logging', module: 'commons-logging'
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    }
}

gradle.taskGraph.beforeTask { Task task ->
    if (task instanceof Javadoc) {
        def releaseVariant = android.libraryVariants.find { it.buildType.name == 'release' }
        Javadoc javadocTask = (Javadoc) task
        javadocTask.source = android.sourceSets.main.java.srcDirs
        ext.androidJar = "${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"
        javadocTask.classpath = files(releaseVariant.javaCompile.classpath.files) + files(ext.androidJar)
    }
}

// Log out test results to console
tasks.matching {it instanceof Test}.all {
    testLogging.events = ["failed", "passed", "skipped"]
}

check.dependsOn 'checkstyle', 'findbugs', 'pmd', 'jacocoTestReport'

apply from: '../installv1.gradle'
apply from: '../bintrayv1.gradle'

install.mustRunAfter check